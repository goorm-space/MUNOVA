import groovy.json.JsonSlurper

pipeline {
    agent any
    tools {
        jdk 'JDK21'
    }

    environment {
        TAG = "${env.BUILD_NUMBER}"
        DOCKER_IMAGE_NAME = 'goorm-space/MUNOVA-api'
        WEBHOOK_URL = credentials("MUNOVA-Jenkins-webhook")
    }

    triggers {
        GenericTrigger(
            genericVariables: [
                [key: 'repository', value: '$.repository.name', defaultValue: 'null'],
                [key: 'repositoryLink', value: '$.repository.html_url', defaultValue: 'null'],
                [key: 'action', value: '$.action', defaultValue: 'null'],
                [key: 'prIsMerged', value: '$.pull_request.merged', defaultValue: 'false'],
                [key: 'prNumber', value: '$.pull_request.number', defaultValue: '0'],
                [key: 'prHtmlLink', value: '$.pull_request.html_url', defaultValue: 'null'],
                [key: 'prTitle', value: '$.pull_request.title', defaultValue: 'null'],
                [key: 'prRequester', value: '$.pull_request.user.login', defaultValue: 'null'],
                [key: 'prLabelList', value: '$.pull_request.labels', defaultValue: '[]'],
                [key: 'mergeTo', value: '$.pull_request.base.ref', defaultValue: 'null'],
                [key: 'mergeFrom', value: '$.pull_request.head.ref', defaultValue: 'null']
            ],
            tokenCredentialId: 'MUNOVA-Github-Webhook-Token',
            printContributedVariables: false,
            printPostContent: false,
            silentResponse: false,
            causeString: 'Triggered by GitHub Pull Request',
            regexpFilterText: '$action',
            regexpFilterExpression: '^(opened|reopened|synchronize|closed)$'
        )
    }

    stages {

        stage('Checkout') {
            steps {
                git branch: 'devtest',
                    url: 'https://github.com/goorm-space/MUNOVA.git',
                    credentialsId: 'MUNOVA-Access-Token'
            }
        }

        stage('Copy application.properties') {
            steps {
                withCredentials([file(credentialsId: 'MUNOVA-SECRET', variable: 'SECRET_FILE')]) {
                    sh '''
                        rm -f src/main/resources/application.properties
                        mkdir -p src/main/resources
                        cp $SECRET_FILE src/main/resources/application.properties
                    '''
                }
            }
        }

        stage('Build') {
            steps {
                sh './gradlew clean build'
            }
        }

        stage('PR Notification') {
            when {
                expression { return env.action != null && env.action != "null" }
            }
            steps {
                script {
                    // ë¼ë²¨ íŒŒì‹±
                    def labels = parseLabels(prLabelList)

                    // PR ìƒíƒœë³„ ë©”ì‹œì§€ ìƒì„±
                    def prMessage = generatePRMessage(
                        action, prIsMerged, prNumber, prHtmlLink,
                        prTitle, prRequester, labels, mergeFrom, mergeTo, repository, repositoryLink
                    )

                    // Discord ì „ì†¡
                    sendDiscord(prMessage, "GitHub Pull Request ì•Œë¦¼")
                }
            }
        }
    }

    post {
        success {
            script { sendBuildResult("ë¹Œë“œ ì„±ê³µ âœ…") }
        }
        failure {
            script { sendBuildResult("ë¹Œë“œ ì‹¤íŒ¨ âŒ") }
        }
    }
}

/////////////////////////////////////////////////////////
// ----------------- FUNCTIONS ------------------------//
/////////////////////////////////////////////////////////

def parseLabels(labelListJson) {
    def jsonSlurper = new JsonSlurper()
    def labelObjects = []
    try {
        labelObjects = jsonSlurper.parseText(labelListJson)
    } catch (e) {
        echo "No labels found"
    }
    return labelObjects.collect { it.name }.join(', ') ?: 'ì—†ìŒ'
}

def generatePRMessage(action, prIsMerged, prNumber, prHtmlLink, prTitle, prRequester, labels, mergeFrom, mergeTo, repository, repositoryLink) {
    def emoji = ':fire:'
    def status = ''

    switch(action) {
        case 'opened': emoji = ':sparkles:'; status = 'Pull Request ìƒì„±'; break
        case 'reopened': emoji = ':recycle:'; status = 'Pull Request ìž¬ì˜¤í”ˆ'; break
        case 'synchronize': emoji = ':arrows_counterclockwise:'; status = 'PR ë‚´ìš© ì—…ë°ì´íŠ¸'; break
        case 'closed':
            if (prIsMerged == "true") { emoji = ':tada:'; status = 'ë³‘í•© ì™„ë£Œ' }
            else { emoji = ':x:'; status = 'PR ë‹«íž˜' }
            break
    }

    return """
        ${emoji} **${status}**
        > ðŸ“˜ [#${prNumber}](${prHtmlLink}) ${prTitle}
        > ðŸ‘¤ ìž‘ì„±ìž: ${prRequester}
        > ðŸ§© ë¼ë²¨: ${labels}
        > ðŸ”€ ë¸Œëžœì¹˜: ${mergeFrom} â†’ ${mergeTo}
        > ðŸ“¦ ì €ìž¥ì†Œ: [${repository}](${repositoryLink})
        """.trim()
        }

def sendDiscord(message, title) {
    discordSend(
        webhookURL: env.WEBHOOK_URL,
        title: title,
        description: message,
        footer: "Job: ${env.JOB_NAME} | Build #${env.BUILD_NUMBER}",
        result: currentBuild.currentResult
    )
}

def sendBuildResult(statusTitle) {
    def commitMsg = sh(script: "git log -1 --pretty=%B", returnStdout: true).trim()
    def commitHash = sh(script: 'git rev-parse HEAD', returnStdout: true).trim()
    def commitUrl = "${env.GIT_URL.replace('.git','')}/commit/${commitHash}"

    discordSend(
        webhookURL: env.WEBHOOK_URL,
        title: statusTitle,
        description: "ì»¤ë°‹ ë©”ì‹œì§€: ${commitMsg}\n[ì»¤ë°‹ ë°”ë¡œê°€ê¸°](${commitUrl})",
        footer: "Job: ${env.JOB_NAME} | Build #${env.BUILD_NUMBER}",
        link: env.BUILD_URL,
        result: currentBuild.currentResult
    )
}